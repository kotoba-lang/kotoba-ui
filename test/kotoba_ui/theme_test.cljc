(ns kotoba-ui.theme-test
  (:require [clojure.test :refer [deftest is testing]]
            [clojure.string :as str]
            [kotoba-ui.theme :as theme]
            [kotoba-ui.core :as ui]))

(defn- count-occurrences [s sub]
  (loop [i 0 n 0]
    (if-let [j (str/index-of s sub i)]
      (recur (inc j) (inc n))
      n)))

(deftest hex->rgba-test
  (is (= "rgba(255,60,172,0.55)" (theme/hex->rgba "#FF3CAC" "0.55")))
  (is (= "rgba(10,132,255,0.85)" (theme/hex->rgba "0A84FF" "0.85")))
  (testing "#RGB shorthand expands"
    (is (= "rgba(255,255,255,1)" (theme/hex->rgba "#fff" "1")))))

(deftest appearance-attr-test
  (is (nil? (theme/appearance-attr nil)))
  (is (nil? (theme/appearance-attr {})))
  (is (nil? (theme/appearance-attr {:appearance :auto})))
  (is (= "light" (theme/appearance-attr {:appearance :light})))
  (is (= "dark" (theme/appearance-attr {:appearance :dark}))))

(deftest accent-threading-test
  (testing ":accent reaches both libraries' emitted vars"
    (let [css (theme/theme-css {:accent "#FF3CAC"})]
      (is (str/includes? css "--hig-color-tint: #FF3CAC;"))
      (is (str/includes? css "--liquid-glass-accent-tint: rgba(255,60,172,0.55);"))
      (is (str/includes? css "--liquid-glass-accent-tint-strong: rgba(255,60,172,0.85);"))))
  (testing ":accent-dark defaults to :accent; when given, it shapes the dark values"
    (let [css (theme/theme-css {:accent "#FF3CAC" :accent-dark "#FF5AC8"})]
      (is (str/includes? css "--hig-color-tint: #FF3CAC;"))
      (is (str/includes? css "--hig-color-tint: #FF5AC8;"))
      (is (str/includes? css "--liquid-glass-accent-tint: rgba(255,90,200,0.55);"))))
  (testing "default theme keeps the HIG system-blue tint"
    (let [css (theme/theme-css nil)]
      (is (str/includes? css "--hig-color-tint: #007AFF;")))))

(deftest override-escape-hatch-test
  (testing "raw :hig / :glass token maps flow through"
    (let [css (theme/theme-css {:hig   {:hig/radius {:md "18px"}}
                                :glass {:liquid-glass/radius {:md "18px"}}})]
      (is (str/includes? css "--hig-radius-md: 18px;"))
      (is (str/includes? css "--liquid-glass-radius-md: 18px;"))))
  (testing "raw :hig wins over the :accent-derived tint (light appearance)"
    (let [theme {:accent "#FF3CAC" :hig {:hig/color {:tint "#112233"}}}]
      (is (= {:hig/color {:tint "#112233"}} (theme/hig-overrides theme)))
      (is (str/includes? (theme/theme-css theme) "--hig-color-tint: #112233;")))))

(deftest theme-css-structure-test
  (let [css  (theme/theme-css {:accent "#FF3CAC"})
        decl "@layer kotoba.hig, kotoba.glass;"]
    (testing "the layer-order declaration appears exactly once, first"
      (is (str/starts-with? css decl))
      (is (= 1 (count-occurrences css decl))))
    (testing "both cascade layers are filled"
      (is (str/includes? css "@layer kotoba.hig {"))
      (is (str/includes? css "@layer kotoba.glass {")))
    (testing "the bundle carries all four parts: vars, base, material, shell"
      (is (str/includes? css "--hig-text-body-font-size"))
      (is (str/includes? css ":focus-visible"))
      (is (str/includes? css ".hig-large-title"))
      (is (str/includes? css ".liquid-glass__button"))
      (is (str/includes? css ".kotoba-shell__hero")))
    (testing "balanced braces"
      (is (= (count-occurrences css "{") (count-occurrences css "}"))))))

(deftest ->page-test
  (let [html (ui/->page {:title "Hello <World>"
                         :description "One-call page."
                         :theme {:accent "#FF3CAC" :appearance :dark}}
                        [:p "body"])]
    (is (str/starts-with? html "<!doctype html>"))
    (is (str/includes? html "name=\"viewport\""))
    (testing "title is escaped"
      (is (str/includes? html "<title>Hello &lt;World&gt;</title>")))
    (is (str/includes? html "data-appearance=\"dark\""))
    (is (str/includes? html "<style>"))
    (is (str/includes? html "--hig-color-tint: #FF3CAC;"))
    (is (str/includes? html "<p>body</p>"))))
